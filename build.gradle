buildscript {
	ext.kotlin_version = '1.6.0'
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

plugins {
	id "org.jetbrains.kotlin.jvm"            version "${kotlinVersion}"
	id "org.jetbrains.kotlin.plugin.spring"  version "${kotlinVersion}"
	id "org.jetbrains.kotlin.plugin.allopen" version "${kotlinVersion}"
	id "org.springframework.boot"            version "${springBootVersion}"
	id "io.spring.dependency-management"     version "${dependencyManagementVersion}"
	id "io.gitlab.arturbosch.detekt"         version "${detektVersion}"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "jacoco"

def jarSuffix = System.getenv('JAR_SUFFIX') ?: ""
description = description
group       = group
version     = version + jarSuffix

// Separando dependencias para outro arquivo.
apply from: 'dependencies.gradle'

repositories {
	mavenCentral()
	gradlePluginPortal()
	mavenLocal()
}

bootJar {
	launchScript()
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs += "-Xjsr305=strict"
		jvmTarget = "1.8"
	}
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs += "-Xjsr305=strict"
		jvmTarget = "1.8"
	}
}

test {
	useJUnitPlatform()
	ignoreFailures = false
	jvmArgs = ["-Duser.timezone=America/Sao_Paulo",
			   "-XX:+UnlockExperimentalVMOptions",
			   "-XX:+UseCGroupMemoryLimitForHeap"]
	finalizedBy(jacocoTestReport)
}

sourceSets {
	main {
		java.srcDir("src/main/kotlin")
	}
	test {
		java.srcDir("src/test/kotlin")
	}
}

detekt {
	toolVersion = detektVersion
	config = files("$projectDir/config/detekt.yml")
	autoCorrect = true
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

jacoco {
	toolVersion = "0.8.7"
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
		html.destination = file("${buildDir}/reports/jacoco/html")
	}
}
