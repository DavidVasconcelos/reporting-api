import org.jetbrains.kotlin.gradle.dsl.JvmTarget

buildscript {
	ext.kotlin_version = "${kotlinVersion}"
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

plugins {
	id "org.jetbrains.kotlin.jvm"            version "${kotlinVersion}"
	id "org.jetbrains.kotlin.plugin.spring"  version "${kotlinVersion}"
	id "org.jetbrains.kotlin.plugin.allopen" version "${kotlinVersion}"
	id "org.jetbrains.kotlin.plugin.jpa"     version "${kotlinVersion}"
	id "org.springframework.boot"            version "${springBootVersion}"
	id "io.spring.dependency-management"     version "${dependencyManagementVersion}"
	id "io.gitlab.arturbosch.detekt"         version "${detektVersion}"
	id "org.flywaydb.flyway"                 version "${flyWayVersion}"
	id "jacoco"
}

apply from: "gradle/git-hooks/git-hooks.gradle"

def jarSuffix = System.getenv('JAR_SUFFIX') ?: ""
description = description
group       = group
version     = version + jarSuffix
java.sourceCompatibility = JavaVersion.VERSION_21

apply from: 'dependencies.gradle'

repositories {
	mavenCentral()
	gradlePluginPortal()
	mavenLocal()
}

jar {
	enabled = false
}

bootJar {
	launchScript()
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict'
		jvmTarget.set(JvmTarget.JVM_21)
	}
}


test {
	useJUnitPlatform()
	ignoreFailures = false
	jvmArgs = ["-Duser.timezone=America/Sao_Paulo",
			   "-XX:+UnlockExperimentalVMOptions"]
	finalizedBy(jacocoTestReport)
}

sourceSets {
	main {
		java.srcDir("src/main/kotlin")
	}
	test {
		java.srcDir("src/test/kotlin")
	}
}

detekt {
	toolVersion = detektVersion
	config.setFrom("$projectDir/config/detekt.yml")
	autoCorrect = true
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

jacoco {
	toolVersion = "0.8.12"
}

jacocoTestReport {
	reports {
		xml.required = false
		csv.required = false
		html.required = true
		html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
	}
}
